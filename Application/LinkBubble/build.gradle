buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
    }
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 9
def versionPatch = 58
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

apply plugin:'com.android.application'

boolean isRelease = false;
for (int i = 0; i < project.gradle.startParameter.taskNames.size(); i++) {
    String item = project.gradle.startParameter.taskNames.get(i);
    if (item.contains("Release")) {
        isRelease = true;
    }
}

repositories {
    mavenCentral()
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 22
    buildToolsVersion '26.0.2'
    flavorDimensions "default"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 22

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        ndk {
            ldLibs "log"
            moduleName "LinkBubble"
            abiFilters "armeabi-v7a", "x86"
        }
    }

    productFlavors {
        playstore {
            applicationId='com.linkbubble.playstore'

            buildConfigField "String", "STORE_PACKAGE", "\"com.android.vending\""
            buildConfigField "String", "STORE_URL_PREFIX", "\"http://play.google.com/store/apps/details?id=\""
            buildConfigField "String", "STORE_URL", "\"http://play.google.com/store/apps/details?id=com.linkbubble.playstore\""
            buildConfigField "String", "STORE_MY_OTHER_APPS_URL", "\"https://play.google.com/store/apps/developer?id=Link+Bubble\""
            buildConfigField "String", "TAP_PATH_PACKAGE_NAME", "\"com.digitalashes.tappath.playstore\""
        }
    }

    signingConfigs {
        release {
            if(isRelease) {
                storeFile file(System.getenv("LINK_BUBBLE_KEYSTORE_LOCATION"))
                storePassword System.getenv("LINK_BUBBLE_KEYSTORE_PASSWORD")
                keyAlias System.getenv("LINK_BUBBLE_KEY_ALIAS")
                keyPassword System.getenv("LINK_BUBBLE_KEY_PASSWORD")
            }
        }
    }
    sourceSets {
        main {
            jni.srcDirs = []
            jniLibs.srcDir 'src/main/libs'
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
            debuggable true
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        lintConfig file("lint.xml")
    }
    externalNativeBuild {
        ndkBuild {
            path 'Android.mk'
        }
    }
}

dependencies {
    implementation 'com.android.support:support-v4:22.2.0'
    implementation "com.android.support:appcompat-v7:22.2.0"
    implementation 'com.android.support:palette-v7:22.2.0'
    implementation 'com.android.support:recyclerview-v7:22.2.0'
    implementation 'com.android.support:design:22.2.0'
    implementation 'se.emilsjolander:stickylistheaders:2.1.2'
    implementation 'com.squareup.retrofit:retrofit:1.5.0'
    implementation 'com.timehop.stickyheadersrecyclerview:library:0.4.0@aar'
    implementation 'com.jakewharton:butterknife:7.0.0'
    annotationProcessor 'com.jakewharton:butterknife:7.0.0'
    implementation 'com.squareup:otto:1.3.8'
    implementation files('libs/picasso-2.1.1.jar')
    implementation files('libs/jsoup-1.7.3.jar')
    implementation files('libs/slf4j-android-1.5.8.jar')
    //implementation project(':Libraries:ViewPagerIndicator:library')
}

task showVersion << {
    println buildscript.configurations.classpath.resolvedConfiguration.firstLevelModuleDependencies.moduleVersion
}

clean.dependsOn showVersion
